<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>Title</title>
</head>
<body>

<script id="2d-vertex-shader" type="x-shader/x-vertex">
attribute vec2 a_position;
  void main() {
    gl_Position = vec4(a_position, 0, 1);
  }
</script>

<script id="2d-fragment-shader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
	precision highp float;
#else
    precision mediump float;
#endif

uniform float iGlobalTime;
uniform vec2 iDimensions;

vec2 CRTCurveUV( vec2 uv )
{
    uv = uv * 2.0 - 1.0;
    vec2 offset = abs( uv.yx ) / vec2( 6.0, 4.0 );
    uv = uv + uv * offset * offset;
    uv = uv * 0.5 + 0.5;
    return uv;
}

void DrawVignette( inout vec3 color, vec2 uv )
{
    float vignette = uv.x * uv.y * ( 1.0 - uv.x ) * ( 1.0 - uv.y );
    vignette = clamp( pow( 16.0 * vignette, 0.3 ), 0.0, 1.0 );
    color *= vignette;
}

void DrawScanline( inout vec3 color, vec2 uv )
{
    float scanline = clamp( 0.95 + 0.05 * cos( 3.14 * ( uv.y + 0.008 * iGlobalTime ) * iDimensions.y * 0.5 ), 0.0, 1.0 );
    float grille = 0.85 + 0.15 * clamp( 1.5 * cos( 3.14 * uv.x * iDimensions.x * 1.0 ), 0.0, 1.0 );
    color *= scanline * grille * 1.2;
}

void main() {
    vec2 uv = gl_FragCoord.xy / iDimensions;
    vec3 color = vec3(uv, 0.0);
    vec2 crtUV = CRTCurveUV(uv);
    if ( crtUV.x < 0.0 || crtUV.x > 1.0 || crtUV.y < 0.0 || crtUV.y > 1.0 ) {
        color = vec3( 0.0, 0.0, 0.0 );
    }
    DrawVignette(color, crtUV);
    DrawScanline(color, uv);
    gl_FragColor = vec4(color, 1.0);
}
</script>

<script type="text/javascript" src="js/main.js"></script>
<canvas id="glscreen"></canvas>
<button id="fullScreen">Full Screen</button>
</body>
</html>